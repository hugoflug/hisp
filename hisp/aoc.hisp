(ns "aoc")

(import "core" ())

(def intmax 1000000)

(defn count-increases
  (depths)
  (let
    (count 
      (fn ((c prev) v)
            (:
              (if (> v prev)
                (+ c 1)
                c)
              v)))
    (head (reduce count (: 0 intmax) depths))))
(assert-equal (count-increases (: 199 200 208 210 200 207 240 269 260 263)) 7)

(defn aoc1-1
  (input)
  (count-increases (map parse-int (split-on input "\n"))))
(assert-equal
  (aoc1-1 (read-file "AOC1.txt"))
  1722)

(defn count-increases-2
  (depths)
  (let
    (count 
      (fn ((c prev) v)
        (let
          (curr (snoc v (drop 1 prev)))
          (:
            (if (> (reduce + (head curr) (tail curr)) (reduce + (head prev) (tail prev)))
              (+ c 1)
              c)
            curr))))
    (head (reduce count (: 0 (take 3 depths)) (drop 3 depths)))))
(assert-equal (count-increases-2 (: 199 200 208 210 200 207 240 269 260 263)) 5)

(defn aoc1-2
  (input)
  (count-increases-2 (map parse-int (split-on input "\n"))))
(assert-equal
  (aoc1-2 (read-file "AOC1.txt"))
  1748)

(defn parse-line
  (line)
  (let ((command unit) (split-on line " "))
    (: command (parse-int unit))))
(assert-equal
  (parse-line "up 5")
  (: "up" 5))

(defn move
  ((depth pos) (command unit))
  (case command
    "forward" (: depth (+ pos unit))
    "up" (: (- depth unit) pos)
    "down" (: (+ depth unit) pos)))

(defn calc-pos
  (instr)
  (let ((depth pos) (reduce move (: 0 0) instr))
    (* depth pos)))

(defn aoc2-1
  (input)
  (calc-pos (map parse-line (split-on input "\n"))))
(assert-equal
  (aoc2-1 (read-file "AOC2.txt"))
  1654760)

(defn move-2
  ((aim depth pos) (command unit))
  (case command
    "forward" (: aim (+ depth (* unit aim)) (+ pos unit))
    "up" (: (- aim unit) depth pos)
    "down" (: (+ aim unit) depth pos)))

(defn calc-pos-2
  (instr)
  (let ((_ depth pos) (reduce move-2 (: 0 0 0) instr))
    (* depth pos)))

(defn aoc2-2
  (input)
  (calc-pos-2 (map parse-line (split-on input "\n"))))
(assert-equal
  (aoc2-2 (read-file "AOC2.txt"))
  1956047400)
