(def defmacro 
  (macro (name args body) 
    (' (def (~ name) (macro (~ args) (~ body))))))

(defmacro defn
  (name args body) 
  (' (def (~ name) (fn (~ args) (~ body)))) )

(defn assert-equal
  (a b)
  (if (= a b)
    nil
    (error (str a " was not equal to " b))))

(defn map
  (f list)
  (if (= list ())
    ()
    (cons 
      (f (head list))
      (map f (tail list)))))

(defn filter
  (f list)
  (if (= list ())
    ()
    (if (f (head list)) 
      (cons (head list) (filter f (tail list)))
      (filter f (tail list)))))

(defn reduce
  (f val list)
  (if (= list ())
    val
    (reduce f (f val (head list)) (tail list))))
(assert-equal (reduce + 7 (' (1 2 3))) 13)

(defmacro let
  (bindings body)
    (cons
      (' (fn
        (~ (map head bindings))
        (~ body)))
      (map
        (fn (bnd) (head (tail bnd)))
        bindings)))

(defn reverse' (l reverted)
  (if (= l ())
    reverted
    (reverse' (tail l) (cons (head l) reverted))))
(defn reverse 
  (l)
  (reverse' l ()))
(assert-equal (reverse (' (1 2 3))) (' (3 2 1)))

(defn split-on'
  (s c splits curr)
  (if (= s ())
    (reverse (cons (apply str (reverse curr)) splits))
    (if (= (head s) c)
      (split-on' (tail s) c (cons (apply str (reverse curr)) splits) (' ()))
      (split-on' (tail s) c splits (cons (head s) curr)))))
(defn split-on
  (s c)
  (split-on' (split s) c () ()))
(assert-equal (split-on "a,b,c" ",") (' ("a" "b" "c")))
(assert-equal (split-on "ax,bx,cx" ",") (' ("ax" "bx" "cx")))

(defn parse-digit
  (s)
  (if (= s "1") 1
    (if (= s "2") 2
      (if (= s "3") 3
        (if (= s "4") 4
          (if (= s "5") 5
            (if (= s "6") 6
              (if (= s "7") 7
                (if (= s "8") 8
                  (if (= s "9") 9
                    (if (= s "0") 0 nil)))))))))))
(assert-equal (parse-digit "8") 8)

(defn parse-int'
  (l)
  (if (= l ())
    0
    (+
      (head l)
      (* 10 (parse-int' (tail l))))))
(defn parse-int
  (s)
  (parse-int' (reverse (map parse-digit (split s)))))
(assert-equal (parse-int "873") 873)
(assert-equal (parse-int "901") 901)
(assert-equal (parse-int "0") 0)

