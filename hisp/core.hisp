(def defmacro 
  (macro (name args body) 
    (' (def (~ name) (macro (~ args) (~ body))))))

(defmacro defn
  (name args body) 
  (' (def (~ name) (fn (~ args) (~ body)))) )

(defn assert-equal
  (a b)
  (if (= a b)
    nil
    (error (str a " was not equal to " b))))

(defn list (& l) l)
(assert-equal (list 1 2 3) (' (1 2 3)))
(assert-equal (list) (' ()))

(defn do (& l) nil)
(assert-equal (do 1 2 3) nil)

(defn map
  (f list)
  (if (= list ())
    ()
    (cons 
      (f (head list))
      (map f (tail list)))))

(defn filter
  (f list)
  (if (= list ())
    ()
    (if (f (head list)) 
      (cons (head list) (filter f (tail list)))
      (filter f (tail list)))))

(defn reduce
  (f val list)
  (if (= list ())
    val
    (reduce f (f val (head list)) (tail list))))
(assert-equal (reduce + 7 (list 1 2 3)) 13)

(defmacro let
  (bindings body)
    (cons
      (' (fn
        (~ (map head bindings))
        (~ body)))
      (map
        (fn (bnd) (head (tail bnd)))
        bindings)))

(defn or
  (a b)
  (nand (nand a a) (nand b b)))
(assert-equal (or false false) false)
(assert-equal (or false true) true)
(assert-equal (or true false) true)
(assert-equal (or true true) true)

(defn reverse' (l reverted)
  (if (= l ())
    reverted
    (reverse' (tail l) (cons (head l) reverted))))
(defn reverse 
  (l)
  (reverse' l ()))
(assert-equal (reverse (list 1 2 3)) (list 3 2 1))

(defn split-on'
  (s c splits curr)
  (if (= s ())
    (reverse (cons (apply str (reverse curr)) splits))
    (if (= (head s) c)
      (split-on' (tail s) c (cons (apply str (reverse curr)) splits) (' ()))
      (split-on' (tail s) c splits (cons (head s) curr)))))
(defn split-on
  (s c)
  (split-on' (split s) c () ()))
(assert-equal (split-on "a,b,c" ",") (list "a" "b" "c"))
(assert-equal (split-on "ax,bx,cx" ",") (list "ax" "bx" "cx"))

(defn parse-digit
  (s)
  (if (= s "1") 1
    (if (= s "2") 2
      (if (= s "3") 3
        (if (= s "4") 4
          (if (= s "5") 5
            (if (= s "6") 6
              (if (= s "7") 7
                (if (= s "8") 8
                  (if (= s "9") 9
                    (if (= s "0") 0 nil)))))))))))
(assert-equal (parse-digit "8") 8)

(defn parse-int'
  (l)
  (if (= l ())
    0
    (+
      (head l)
      (* 10 (parse-int' (tail l))))))
(defn parse-int
  (s)
  (parse-int' (reverse (map parse-digit (split s)))))
(assert-equal (parse-int "873") 873)
(assert-equal (parse-int "901") 901)
(assert-equal (parse-int "0") 0)

(defn take 
  (n l)
  (if (or (= n 0) (= l ()))
      ()
      (cons (head l) (take (- n 1) (tail l)))))
(assert-equal (take 3 (list 1 2 3 4 5)) (list 1 2 3))
(assert-equal (take 3 (list 1 2)) (list 1 2))
(assert-equal (take 3 (list)) (list))

(defn drop 
  (n l)
  (if (= n 0)
    l
    (if (= l ())
      ()
      (drop (- n 1) (tail l)))))
(assert-equal (drop 3 (list 1 2 3 4 5)) (list 4 5))
(assert-equal (drop 3 (list 1 2)) (list))
(assert-equal (drop 3 (list)) (list))

(defn snoc 
  (elem l)
  (if (= l ())
    (list elem)
    (cons (head l) (snoc elem (tail l)))))
(assert-equal (snoc 3 (list 1 2)) (list 1 2 3))
(assert-equal (snoc 3 (list)) (list 3))
